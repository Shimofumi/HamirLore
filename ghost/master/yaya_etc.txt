//******************************************************************************
// YAYA　ゴーストテンプレート
// 種々のイベント辞書
//******************************************************************************

//******************************************************************************
//自己情報通知
//******************************************************************************
OnNotifySelfInfo
{
	if reference[3] == 'ちいさなへーちゃんさま' {
		mode = 'SmallHEChan'
	}
	elseif reference[3] == 'もしも大学七回生だったら' {
		mode = 'Daigaku7kaisei'
	}
	else {
		mode = ''
	}
}

//******************************************************************************
//シェル切り替え
//******************************************************************************

//------------------------------------------------------------------------------
//シェルチェンジ開始
//------------------------------------------------------------------------------

OnShellChanging
{
	case reference[0] 
	{
		when "ちいさなへーちゃんさま" 
		{
			"\0\s[4]御覧じろ！\w5年をとどめて老いざらしむ丹法だ！\w5\n…\w5…\w5真似事だけどね。\e"
			"\0\s[21]白菊花汁、地椿汁、樗汁。\w9\n全部混ぜるとむっちゃ可愛い女の子ができる……ハズ？\e"
			"\0\s[9]お砂糖、スパイス、怖いおもちゃを少々\w5\n…\w5…\w5ついでに巴沙と汞も入れてみようか。\e"
		}
		when "The Keys to December"
		{
			"\0\s[0]丹法を集めていた知り合いがいてね、\w5抱朴子だったかな。\w9今も残ってるといいけど。\e"
			"\0\s[21]この身は逍遥自在なれば、\w5また酔えば夢を見るし、あるいは夢がわたしを見ているのか。\e"
			"\0\s[7]見よう見まねの丹法は、\w5てくのろじーじゃないよ。\w9\nむーでぃーに着替えてるだけだよ。"
		}
		when "もしも大学七回生だったら"
		{
			"\0\s[0]京都のほうの大学生にジョブチェンジします。\w9貴君もだよ。\e"
			"\0\s[21]この身は逍遥自在なれば、\w5また酔えば夢を見るし、あるいは夢がわたしを見ているのか。\e"
			"\0\s[7]あの服はね、\w5ある友達がくれたんだ。\w9\nちょっと着替えてくるよ。"
		}
		others
		{
			"\0\s[4]御覧じろ！\w5なんやかんやでなんやかんやする丹法だ！\w5\n…\w5…\w5真似事だけどね。\e"
			"\0\s[0]いい感じの服をくれた人がいてね。着替えてくるよ。\e"
			"\0\s[0]目の前にあるものを掴めるという感覚、それが大切なんだよ\e"
			"\0\s[0]我々は夢と同じもので出来ている――\w9\nそういった友人がいたよ。\e"
		}
	}
}

//------------------------------------------------------------------------------
//シェルチェンジ完了
//------------------------------------------------------------------------------
OnShellChanged
{
	
	case reference[0] 
	{
		when "The Keys to December" 
		{
			"\0\s[0]とりとめのない夢を見ていた。\w9いや、こっちが夢かな？\w9\nどちらでもいいよ。うん、どちらでもいいんだよ。\e"
			"\0\s[0]この不老の身（感覚には種族差があります）には、\w9\n仙丹も小さな器に満ちた酒も、\w5同じようなものだよ。\e"
			"\0\s[21]やっぱり、いつもので。\w9\s[5]\nそれでは、一日を変え、\w5一生を変える一杯を！\e"
		}
		when "ちいさなへーちゃんさま"
		{
			"\0\s[0]ああ、この大地は、天球は、こんなにも果てなく広かったのだなあ。\e"
			"\0\s[4]おーい、\w5おーい。\w5した見て。しーたーだーよー！\w9\nおはよう、おっきな%(username)。\e"
			"\0\s[21]どんな丹法かもわからないまま適当にやってみたけど、これなら。\w5まあ、\w5まあ？\e"
		}
		when "もしも大学七回生だったら"
		{
			"\0\s[0]とりとめのない夢を見ていた。\w9いや、こっちが夢かな？\w9\nどちらでもいいよ。うん、どちらでもいいんだよ。\e"
			"\0\s[0]うはははは、大学生だ、自由だ。\e"
			"\0\s[0]この不老の身（感覚には種族差があります）には、\w9\n一浪も多浪も留年も、\w5同じようなものだよ。\e"
			"\0\s[0]ダウナーフォームとも呼ばれていたね、\w9この格好は。"
		
		}
		others 
		{
			"\0\s[7]変身！%(reference[0])！\w8\s[5]\n似合うかな？\e"
			"\0\s[0]とりとめのない夢を見ていた。\w9いや、こっちが夢かな？\w9\nどちらでもいいよ。うん、どちらでもいいんだよ。\e"
			"\0\s[21]どんな丹法かもわからないまま適当にやってみたけど、これなら。\w5まあ、\w5まあ？\e"
		}
	}
}


//******************************************************************************
//インストール関連
//******************************************************************************

//------------------------------------------------------------------------------
//インストール中のメッセージ
//------------------------------------------------------------------------------
OnInstallBegin
{
	"\0\s[0]インストールしています…\w6…\w6\e"
}

//------------------------------------------------------------------------------
//インストール完了メッセージ
//------------------------------------------------------------------------------
OnInstallComplete
{
	_ref0 = SPLIT(reference[0],' with',2)[0] //hogehoge with hemohemo
	
	"\0\s[5]"
	--
	if _ref0 == "shell" {
		"シェル"
	}
	elseif _ref0 == "ghost" {
		"ゴースト"
	}
	elseif _ref0 == "balloon" {
		"バルーン"
	}
	elseif _ref0 == "plugin" {
		"プラグイン"
	}
	--
	"「%(reference[1])」、インストール完了ですよー。\n"
	--
	if basewarenameex == 'SSP' {
		_id = ''
		if _ref0 == "shell" {
			_id = 'shell'
		}
		elseif _ref0 == "ghost" {
			_id = 'ghost'
		}
		elseif _ref0 == "balloon" {
			_id = 'balloon'
		}
		
		if _id != '' {
			"切り替えますか？"
			--
			AYATEMPLATE.MenuItem("切り替える","OnAYATEMPLATE.InstallChange",_id,200)
			--
			AYATEMPLATE.MenuItem("切り替えない","Menu_CANCEL")
		}
	}
}

OnAYATEMPLATE.InstallChange
{
	"\![change,%(reference[0]),lastinstalled]"
}

//------------------------------------------------------------------------------
//インストール失敗メッセージ
//------------------------------------------------------------------------------
OnInstallFailure
{
	"\0\s[4]インストールに失敗しました…\w5…\w5\n\n理由：%(reference[0])。ああ……\e"
}

//------------------------------------------------------------------------------
//インストール拒否（他のゴーストのためのアーカイブだった）
//------------------------------------------------------------------------------
OnInstallRefuse
{
	"\0\s[0]むっ、これは、%(reference[0])用のアーカイブだ。\w6処理中断っ。\e"
}


//******************************************************************************
//消滅関連
//******************************************************************************

//------------------------------------------------------------------------------
//消滅指示
//------------------------------------------------------------------------------
OnVanishSelecting
{
	"\0\s[20]ほんとうに？\w9\e"
}

//------------------------------------------------------------------------------
//消滅開始
//------------------------------------------------------------------------------
OnVanishSelected
{
	"\0\s[21]あなたを送り行き、別れの杯を口に含んだときの、なんと味気なかったことか。\n\w9雲の彼方、双月の彼方を望んではいつまでも立ち尽くそう。\w9\n\nさよなら。\e"
}

//------------------------------------------------------------------------------
//消滅中止
//------------------------------------------------------------------------------
OnVanishCancel
{
	"\0\s[21]それもそれでいいよ。うん。\e"
}

//------------------------------------------------------------------------------
//他のゴーストが消滅して自分に切り替わった
//------------------------------------------------------------------------------
OnVanished
{
	"\0\s[2]以前、だれぞやに聞きました。「ここに住んでいたひとはどこへいってしまったのか」と。\w9\n\nその人は言った。「みんな死んでしまってひとりも残っていませんよ」と。\w9\s[20]\n\n人生はまぼろしにも似て、けっきょくは無に帰してしまうのだろうか。\e"
}

//******************************************************************************
//メールチェックはオミット
//******************************************************************************


//******************************************************************************
//ネットワーク更新
//******************************************************************************

//------------------------------------------------------------------------------
//ネットワーク更新開始
//------------------------------------------------------------------------------
OnUpdateBegin
{
	"\0\s[0]\_q更新を積み重ねよう。騏驥も一躍に十歩すること能わず\e"
	"\0\s[0]\_q更新を積み重ねよう。蹞歩(きほ)を積まざれば、以って千里に至るなし。\e"
	"\0\s[0]\_q更新を積み重ねよう。功在不舎\e"
	"\0\s[0]\_q更新を積み重ねよう。牛歩千里\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新準備完了
//------------------------------------------------------------------------------
OnUpdateReady
{
	"\0\s[0]\_q%(TOINT(reference[0])+1)個のファイルをダウンロードします。\e"
	"\0\s[7]\_q%(TOINT(reference[0])+1)個のファイルを%(username)のハードディスクに不法侵入させる開始ー。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新終了
//------------------------------------------------------------------------------
OnUpdateComplete
{
	if reference[0] == "none"
	{
		//---- 更新すべきファイルが無かった
		"\0\s[7]\_qあいや、対象ファイルが無いアルねー。\e"
		"\0\s[20]\_q更新すべきファイルが無かったよ。\e"
		"\0\s[4]\_q多分更新が無かったんじゃないかな。ぶっちゃけ荀子は、その弟子たちがあまりすきくなくてね。\e"
	}
	else
	{
		//---- 実際に更新が完了した
		"\0\s[0]\_q更始。\e"
		"\0\s[4]\_q更始。いやべつに更始帝に思い入れはないよ。\e"
		"\0\s[7]\_q更始。いやべつに五胡十六国ではないが。\e"
	}
}

//------------------------------------------------------------------------------
//ネットワーク更新失敗
//------------------------------------------------------------------------------
OnUpdateFailure
{
	if reference[0] == "timeout"
	{
		//---- タイムアウトした
		"\0\s[0]タイムアウト"
	}
	elseif reference[0] == "md5 miss"
	{
		//---- MD5値が一致しなかった
		"\0\s[0]\_qMD5値が違うんだって。お手数だが、配布元に報告して貰えるとありがたい。\n\nうかどんでもいいよ。"
	}
	else
	{
		//---- 上記以外の理由で失敗した
		"\0\s[0]%(reference[0])、だって。\w5\s[20]うーん。"
	}
	--
	"\n\n\0\s[20]というわけで、ネットワーク更新、失敗だ。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 ダウンロード開始
//------------------------------------------------------------------------------
OnUpdate.OnDownloadBegin
{
	"\0\s[5]\_qファイル「%(reference[0])」をダウンロードします。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値照合開始
//------------------------------------------------------------------------------
// OnUpdate.OnMD5CompareBegin { "\0\s[1]合ってるかな‥\w5‥\w5\w8\1\e" }

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値一致
//------------------------------------------------------------------------------
// OnUpdate.OnMD5CompareComplete {	"\0\s[5]ん、オッケー。\e"}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値不一致
//------------------------------------------------------------------------------
// OnUpdate.OnMD5CompareFailure {	"\0\s[1]たぶんなんかがエラー。\e" }

//******************************************************************************
//ヘッドラインセンス
//******************************************************************************

//------------------------------------------------------------------------------
//ヘッドラインセンス開始
//------------------------------------------------------------------------------
OnHeadlinesenseBegin
{
	"\0\s[0]%(reference[0])ヘッドライン取得中。\e"
}

//------------------------------------------------------------------------------
//ヘッドライン読み上げ
//------------------------------------------------------------------------------
OnHeadlinesense.OnFind
{
	//---- はじめのセリフ
	if reference[2] == "First" || reference[2] == "First and Last"
	{
		//---- 最初のページ
		"\0\b2\s[0]%(reference[0])のヘッドラインね。"
	}
	elseif reference[2] == "Next"
	{
		//---- 真ん中あたりのページ
		"\0\b2\s[0]%(reference[0])ヘッドライン、続き。"
	}
	elseif reference[2] == "Last"
	{
		//---- 最後のページ
		"\0\b2\s[0]%(reference[0])ヘッドライン、これが最後のページです。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%(reference[3])\n\n"
	--
	//---- 選択肢
	if reference[2] == "First" || reference[2] == "Next"
	{
		//---- 次のページがある場合
		AYATEMPLATE.MenuItem("次のページ","")
		--
		AYATEMPLATE.MenuItem("サイトへ",reference[1])
		--
		AYATEMPLATE.MenuItem("やめ","Menu_HEADLINECANCEL",150)
	}
	else
	{
		//---- 次のページがない場合
		AYATEMPLATE.MenuItem("サイトへ",reference[1])
		--
		AYATEMPLATE.MenuItem("やめ","Menu_HEADLINECANCEL",150)
	}
}

//------------------------------------------------------------------------------
//ヘッドライン更新が無かった
//------------------------------------------------------------------------------
OnHeadlinesenseComplete
{
	"\0\s[0]%(reference[0])、更新はないみたいね。\e"
}

//------------------------------------------------------------------------------
//ヘッドラインセンス失敗
//------------------------------------------------------------------------------
OnHeadlinesenseFailure
{
	"\0\s[0]ヘッドラインセンスに失敗しました。\n\n理由："
	--
	if reference[0] == "can't download"
	{
		"ダウンロード失敗。"
	}
	elseif reference[0] == "can't analyze"
	{
		"ヘッドライン解析失敗。"
	}
	else
	{
		"謎。"
	}
	--
	"\e"
}


//******************************************************************************
//ファイル作成関連
//******************************************************************************

//------------------------------------------------------------------------------
//updates2.dau作成開始
//------------------------------------------------------------------------------
OnUpdatedataCreating
{
	"\0\s[5]アップデートデータを作成しています。\e"
}

//------------------------------------------------------------------------------
//updates2.dau作成完了
//------------------------------------------------------------------------------
OnUpdatedataCreated
{
	"\0\s[5]アップデートデータ、ちゃんと作れたよ。\w8よかよか。\e"
}

//------------------------------------------------------------------------------
//NAR作成開始
//------------------------------------------------------------------------------
OnNarCreating
{
	"\0\s[5]narファイルを作成するよ。\e"
}

//------------------------------------------------------------------------------
//NAR作成完了
//------------------------------------------------------------------------------
OnNarCreated
{
	"\0\s[5]「%(reference[0])」のnarファイル、「%(reference[1])」が完成。\e"
}

//******************************************************************************
//時計合わせ
//みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏
//******************************************************************************

//------------------------------------------------------------------------------
//時計合わせ開始
//------------------------------------------------------------------------------
OnSNTPBegin
{
	"\0\s[0]時計合わせ開始。\w8\1%(reference[0])に接続するぞ。\e"
}

//------------------------------------------------------------------------------
//時刻取得成功、比較開始
//この関数で 2008/03/25 19:15:05 のように見やすい形に作り変えます。
//詳しくはSTRFORM関数の説明を見てね！
//わからなければこのままで。
//------------------------------------------------------------------------------

SNTPCompare_StrForm
{
	_txt = SPLIT(_argv[0],',')
	STRFORM("$04d/$02d/$02d $02d:$02d:$02d",TOINT(_txt[0]),TOINT(_txt[1]),TOINT(_txt[2]),TOINT(_txt[3]),TOINT(_txt[4]),TOINT(_txt[5]))
}

OnSNTPCompare
{
	"\0\s[0]\_q/
	修正前 %(SNTPCompare_StrForm(reference[2]))\n/
	修正後 %(SNTPCompare_StrForm(reference[1]))\n/
	\_q\n"
	--
	if reference[3] == 0
	{
		"ぴったり。\e"
	}
	else
	{
		//\6は補正実行タグ。消しちゃだめ
		"\6\0\n\s[5]%(reference[3])秒のずれを修正したよ。"
	}
}

//------------------------------------------------------------------------------
//時計合わせ失敗
//------------------------------------------------------------------------------
OnSNTPFailure
{
	"\0\s[4]…\w5…\w5時計合わせ失敗。\e"
}


//******************************************************************************
//そのほか
//******************************************************************************

//------------------------------------------------------------------------------
//ディスプレイ解像度変更
//------------------------------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference[0]
	displaywidth  = reference[1]
	displayheight = reference[2]
	SHIORI3FW.RegisterTempVar('displaybpp', 'displaywidth', 'displayheight')
}

//------------------------------------------------------------------------------
//URLドロップ
//CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます
//------------------------------------------------------------------------------
OnURLDropping
{
	"\0\s[0]%(reference[0])からデータを取得してるよ。\e"
}

//------------------------------------------------------------------------------
//起動時に MATERIA を検出
//------------------------------------------------------------------------------
OnEmbryoExist
{
	"\0\s[0]あれま、「伺か」が起動してるよ。\w8\s[20]ありがたやありがたや。\e"
}



//------------------------------------------------------------------------------
//ルビ入れるための関数
//------------------------------------------------------------------------------
//例
//text
//{
//    '\0てすと\n\n[half]てすと２' + setRuby('後悔', 'こうかい') + 'てすと\nてすと３'
//    --
//    '\0てすと\n\n[half]てすと２' + setRuby('百舌鳥', 'もず', '1') + 'てすと\nてすと３'
//}
//あーるでぃーさん
//鯖：ゴーストが作りたい各位
//うかどん各位
//多大なる感謝を

// ルビ入れる行は頭に\n[half]を入れること。
// _argv[0] 本体
// _argv[1] ルビ
// _argv[2] 瞬間表示フラグ 0 or 1
setRuby
{
    _text = _argv[0]
    _ruby = _argv[1]
    
    _ret = ''
    // 瞬間表示開始
    if ( _argv[2] == '1' )
    {
        _ret += '\_q'
    }
    
    // 文字サイズはルビ90%計算
    _lentext = TOREAL( STRLEN( _text ) )
    _lenruby = TOREAL( STRLEN( _ruby ) ) * 0.9
    _len = _lentext
    if ( _lentext < _lenruby )
    {
        _len = _lenruby
    }
    
    // 本文の方が長いかどうか
    _start = (_len - _lentext) / 2
    if ( _start < 0 )
    {
        _start = 0
    }
    
    // 本文表示
    _ret += '\_l[@' + _start + 'em,@0em]' + _text + '\_l[@-' + (_start + _lentext) + 'em,@0em]'
    
    // ルビの方が長いかどうか
    _start = (_len - _lenruby) / 2
    if ( _start < 0 )
    {
        _start = 0
    }
    
    // ルビ表示
    _ret += '\_l[@0em,@-0.8em]'
    _ret += '\_l[@' + _start + 'em,@0em]\f[height,75%]' + _ruby + '\_l[@-' + STRLEN( _ruby ) + 'em,@0em]'
    
    // 文字間隔はルビ75%計算
    _lentext = TOREAL( STRLEN( _text ) )
    _lenruby = TOREAL( STRLEN( _ruby ) ) * 0.75
    _len = _lentext
    if ( _lentext < _lenruby )
    {
        _len = _lenruby
    }
    
    // 元の位置にカーソルを戻す
    _ret += '\f[height,default]\_l[@-' + _start + 'em,@0em]\_l[@0em,@0.8em]\_l[@' + _len + 'em,@0em]'
    
    // 瞬間表示終了
    if ( _argv[2] == '1' )
    {
        _ret += '\_q'
    }
    
    _ret
}

//******************************************************************************
//モードチェンジ関連
//******************************************************************************


OnGhostEventTranslate.ETC
{
    _fname = _argv[0] + mode
    if ISFUNC(_fname) {
        _fname
        return
    }
}

//------------------------------------------------------------------------------
//モードチェンジ
//------------------------------------------------------------------------------