//******************************************************************************
// YAYA　ゴーストテンプレート
// 起動/終了トーク辞書
//******************************************************************************

//******************************************************************************
//初回起動 OnFirstBoot
//******************************************************************************

OnFirstBoot
{
	//---- 変数の初期化
	username         = "ユーザ"
	teachusername    = 0	// ユーザー名覚え用

	_vanishcount = reference[0]

	//---- Vanishカウントを見て、真の初回か否かを判定する
	if _vanishcount <= 0
	{
		//---- ほんとうに初めての起動
		"\0\s[10]\1\s[10]あなたは祖父のガラクタを整理していると、\w9\n曐閒電信機と書かれた箱を見つけた。\w8戯れに触ってみると、\w5起動してしまったようだ。\w8\n\n……\n\w8……\w5……\w9\n/
		\0\s[2]空間に人影が浮かぶ。\w8\s[0]\n「それ」はこちらを見るとしばし視線を泳がせたあと、口を開いた。\w9\w9\s[0]\nやあ。\w5はじめましてかな？\w9\s[7]\nわたしは\w3ヘイミル・ロゥ\w9\nしばし歓談しよう。\w9\nところで、\w5貴君は何という名前かな？\![open,inputbox,OnInputNameFirst,-1,ユーザ]\e"
	}
	elseif _vanishcount == 1
	{
		//vanish１回なら誤射かもしれない
		"\0\s[0]やあ。\w5どうも懐旧の情が催されるね。\w9\s[7]\nわたしは\w3ヘイミル・ロゥ\w9\nしばし歓談しよう。\w9\nところで、\w5貴君は何という名前かな？\![open,inputbox,OnInputNameFirst,-1,ユーザ]\e"
	}
	elseif _vanishcount < 10
	{
		//2-9回
		"\0\s[0]やあ。\w5どうも懐旧の情が催されるね。\w9\s[7]\nわたしは\w3ヘイミル・ロゥ\w9\nしばし歓談しよう。\w9\w5\n名前は重要なのさ。\w9\n「それ」が目の前になくとも\w9「それ」を思うことができる。\w9\nそう、名前があればね。\w9\n貴君は何という名前かな？\![open,inputbox,OnInputNameFirst,-1,ユーザ]\e"
	}
	elseif _vanishcount < 100
	{
		//10-99回
		"\0\s[0]やあ。\w5どうも懐旧の情が催されるね。\w9\s[7]\nわたしは\w3ヘイミル・ロゥ\w9\nしばし歓談しよう。\w9\w5\n名前は重要なのさ。\w9\n「それ」が目の前になくとも\w9「それ」を思うことができる。\w9\nそう、名前があればね。\w9\n貴君は何という名前かな？\![open,inputbox,OnInputNameFirst,-1,全裸中年男性さん]\e"
	}
	else
	{
		//100回以上（やりすぎの例）
		"\0\s[0]‥\w5‥\w5さすがに教えて。\w9\nなにがしたいんですか。\w9\nとりあえず名前を名乗れ\w9…\w9…\w9と言いたいところだがもういいよね、\w9\n全裸中年男性さん。\w9\w9\nそれでも泣いて謝るっていうなら、\w5また名前を覚えてあげる。\w9\nさ、また名乗れ。\![open,inputbox,OnInputNameFirst,-1,全裸中年男性さん]\e"
	}
}

//ユーザの名前を覚える

OnInputNameFirst
{
	username = reference[0]
	
	if username == ""
	{
	//空打ちされた
	"名前ぐらい名乗られよ。\![open,inputbox,OnInputNameFirst,-1,ユーザ]"
	}
	else
	{
	 "\0ふむ\w5%usernameか、\w5良い名前だね。"
	}

}



//******************************************************************************
//通常の起動 OnBoot
//******************************************************************************

OnBoot
{
	//---- まず双方のサーフィスを表示
	"\0\s[0]\1\s[10]"
	--
	//---- 起動時間を確認してあいさつを変化
	_timeslot = GetTimeSlot
	
	case _timeslot{
	when "朝"
	{
		"\0\s[4]おはよう。\w5今日はどうするのかな？\e"
		"\0\s[7]やあ、\w5調子はいかがかな。%(username)\e"
		"\0\s[5]おはよう。\w5朝は食べた？\e"
	}
	when "昼"
	{
		"\0\s[4]おはよう。\w5調子はいかがかな。%(username)\e"
		"\0\s[5]おはよう。\w5調子はいかがかな。%(username)\e"
		"\0\s[7]おはよう。\w5調子はいかがかな。%(username)\e"

	}
	when "日中"
	{
		"\0\s[5]おはよう、%(username)。\w9いつでもおはようだよ。\e"
		"\0\s[7]おはよう、%(username)。\w9調子はいかがかな。\e"
	}
	when "夜"
	{
		"\0\s[5]お、おはよう。\w5貴君も一献傾けるかい？\e"
		"\0\s[4]おはよう、%(username)。\w5顔が見れて嬉しいよ。\e"
	}
	others
	{
		//それ以外の時間の場合
		"\0\s[5]おはよう。\w5もう遅いね。\w5それでもいいよ。\e"
		"\0\s[5]おはよう。\w5そしておつかれさま。\w5待ってたよ。\e"
	}
	}//caseの終わり
}

//------------------------------------------------------------------------------
//朝/昼/夜などをおおざっぱに調べる関数
//------------------------------------------------------------------------------
GetTimeSlot
{
	case hour{
	when 22,23,0,1,2,3 {'深夜'}
	when 4,5,6,7,8,9,10,11 {'朝'}
	when 12,13 {'昼'}
	when 14,15,16,17 {'日中'}
	when 18,19,20,21 {'夜'}
	}//caseの終わり
}


//******************************************************************************
//終了 OnClose
//******************************************************************************

OnClose
{
	"\1\s[10]"
	--
	//この部分には\eつけないでね！
	_timeslot = GetTimeSlot
	if _timeslot == "朝"
	{
		//---- 朝に終了
		"\0\s[4]またね。\w5出かけてくるよ。"
		
		"\0\s[7]それじゃ。\w5またね。"
	}
	elseif _timeslot == "昼" || _timeslot == "日中"
	{
		//---- 昼/日中に終了
		"\0\s[4]それじゃ。\w5またね。"
		
		"\0\s[7]うむうむ。\w5では。"
	}
	elseif _timeslot == "夜"
	{
		"\0\s[7]じゃあね。\w5またおりおりに音信だけでも寄せられよ。"
		
		"\0\s[4]それじゃ。\w5わたしは一献傾けてくるよ。"
	}
	else
	{
		//---- 夜/深夜に終了
		"\0\s[7]お疲れ様、またね。"
		"\0\s[0]おやすみ。"
	}
	--
	"\w9\w9\-\e"
}


//******************************************************************************
//最小化からの復帰 OnWindowStateRestore
//******************************************************************************

//OnWindowStateRestore{}
